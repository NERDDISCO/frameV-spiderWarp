<!DOCTYPE html>
<html>
  <head>
    <title>SpiderWarp</title>
    <meta charset="UTF-8" />
  </head>

  <body>
    <canvas></canvas>

    <style type="text/css">
        body, html {
            margin: 0;
        }
        
        canvas {
            width: 100vw;
            height: 100vh;
        }
    </style>

    <script type="module" async>
          window.addEventListener('load', () => {
            (async () => {
              const { default: GLea } = await import ('./glea.js');

              const vert = `
precision highp float;
attribute vec2 position;

void main() {
  gl_Position = vec4(position, 0, 1.0);
}
`;

const frag = `
precision lowp float;
float PI = 3.14159;
uniform float time;
uniform vec2 resolution;

// amount of iterations for raymarching
const int steps = 3;
float smallNumber = 0.0001;
float maxDist = 1.15;

vec2 uv(){return (gl_FragCoord.xy / resolution * 2.0 -1.0) * vec2(resolution.x/resolution.y, 1.0);}  

// Repeat around the origin by a fixed angle.
// For easier use, num of repetitions is use to specify the angle.
float pModPolar(inout vec2 p, float repetitions) {
	float angle = 2.*PI/repetitions;
	float a = atan(p.y, p.x) + angle/2.;
	float r = length(p);
	float c = floor(a/angle);
	a = mod(a,angle) - angle/2.;
	p = vec2(cos(a), sin(a))*r;
	// For an odd number of repetitions, fix cell index of the cell in -x direction
	// (cell index would be e.g. -5 and 5 in the two halves of the cell):
	if (abs(c) >= (repetitions/2.)) c = abs(c);
	return c;
}

float rand(const in float n){return fract(sin(n) * 1e4);}
float rand(const in vec2 n) { return fract(1e4 * sin(17.0 * n.x + n.y * 0.1) * (0.1 + abs(sin(n.y * 13.0 + n.x))));}

float noise(float x) {
    float i = floor(x);
    float f = fract(x);
    float u = f * f * (3.0 - 2.0 * f);
    return mix(rand(i), rand(i + 1.0), u);
}

float noise(vec2 x) {
    vec2 i = floor(x);
    vec2 f = fract(x);

    // Four corners in 2D of a tile
    float a = rand(i);
    float b = rand(i + vec2(1.0, 0.0));
    float c = rand(i + vec2(0.0, 1.0));
    float d = rand(i + vec2(1.0, 1.0));

    vec2 u = f * f * (3.0 - 2.0 * f);
    return mix(a, b, u.x) + (c - a) * u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
}

const vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
vec3 hsv2rgb(vec3 c) {
  vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
  return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

float fbm(vec2 x, const in int it) {
    float v = 0.0;
    float a = 0.5;
    vec2 shift = vec2(100);
    // Rotate to reduce axial bias
    mat2 rot = mat2(cos(0.5), sin(0.5), -sin(0.5), cos(0.50));
    for (int i = 0; i < 32; ++i) {
        if(i<it) {
            v += a * noise(x);
            x = rot * x * 2.0 + shift;
            a *= 0.5;
        }
    }
    return v;
}

float scene(vec3 pos) {
    pModPolar(pos.xy, 8.0);
    float ground = (fbm(vec2(pos.x, pos.y), 3)+ 4.5)
                   + cos(pos.x * 5.0 + (time * .2)) / (1.5 + pos.y)
                   + tan(pos.z * 2.0 + (time * .2)) / (-2.0 + pos.x)
    ;
    
    return ground;
}

vec3 trace(vec3 rayOrigin, vec3 dir) {
    vec3 ray = rayOrigin;
    // distance
    float dist = 0.;
    float totalDist = 0.;
    float hue = 0.;
    
    for (int i = 0; i < steps; i++) {
        dist = scene(ray);
        ray = ray + (dist * dir);
        totalDist += dist;
        //hue = cos(ray.x * 5.0 + (time * .2)) / (10.5 + ray.y);
        hue = (5.0 + (time * .2)) / 10.0;
        
        if (dist < smallNumber) {
            return vec3((maxDist - totalDist/maxDist) * hsv2rgb(vec3(hue, 1., .25)));
        }
    }
    
   return vec3(0.0);
}

void main () {
    vec2 uv = uv();
    
    float camY = -2.25;
    
    vec3 camOrigin = vec3(1.0, camY, -1.0);

    vec3 rayOrigin = vec3(uv + camOrigin.xy, camOrigin.z + 1.0);

    vec3 dir = rayOrigin - camOrigin;
    
    vec3 color = vec3(trace(rayOrigin, dir));
    
	gl_FragColor = clamp(vec4(color.r, color.g, color.b, 1.0), 0., 1.0);
}
`;

const glea = new GLea({
  shaders: [
    GLea.fragmentShader(frag),
    GLea.vertexShader(vert)
  ],
  buffers: {
    // create a position attribute bound
    // to a buffer with 4 2D coordinates
    position: GLea.buffer(2, [1, 1,  -1, 1,  1,-1,  -1,-1])
  }
}).create();

function loop(time) {
  const { gl, width, height } = glea;
  glea.clear();
  glea.uniV('resolution', [width, height]);
  glea.uni('time', time * 1e-3);
  gl.drawArrays(gl.TRIANGLE_STRIP, 0, 4);
  requestAnimationFrame(loop);
}

function setup() {
  const { gl } = glea;
  window.addEventListener('resize', () => {
    glea.resize();
  });
  loop(0);
}

setup();
            })();
          });
    </script>
  </body>
</html>